{SCHEME:
 a list is a maybe, with nothing as the empty list
 and (just (head . tail)) as a non-empty list.
 We can use maybe! to decide whether we've got the head or the tail.
}

nothing @empty
(cons! just!) @lcons

{ list_do applies either the first function if the list is empty or the second function to the head and tail if the list is non-empty }
(@f @full_todo @empty_todo empty_todo (dup! car! swap! cdr! full_todo!) f maybe!) @list_do

(@l (nothing) (drop! just!) l list_do!) @head {Either just head or nothing}
(@l (nothing) (swap! drop!) l list_do!) @tail {A list--possibly empty}

(recurse!@self @l (0) (swap! drop! self! 1 +!) l list_do!) recurse!@len

((recurse!@self @to @from (to) (swap! to lcons! self!) from list_do!) recurse! @inner
 empty inner!) @reverse

(recurse!@self @f @l (empty) (swap! f! swap! f self! lcons!) l list_do!) recurse!@map

(recurse!@self @f @l (empty) (@t @h t f self! (h swap! lcons!) () h f! if!) l list_do!) recurse!@filter

(recurse!@self @n (empty) (n n 1 -! self! lcons!) n zero?! if!) recurse!@build

(recurse!@self @n head (tail! n 1 -! self!) n zero?! if!) recurse!@index

{For a list of length n, (n take-n!) and dump should be inverses.}

(recurse!@self @l () self l list_do!) recurse!@dump

(@n empty lcons n times!) @take-n

{Syntactic sugar: [ a , b , c ] list! -- [ ] list! does NOT work, use empty}
true @[
false @,
false @]
((recurse!@self @b (empty) (@h self! h swap! lcons!) b if!) recurse!@inner
 inner! reverse!) @list


{List-based control structure: input is a list of (test . todo) pairs of functions and a default todo function}

(recurse!@self @default @l default
                           (@t @h (h cdr! !)
                                  (t default self!)
                                  h car! ! if!)
                           l list_do!) recurse!@switch
